;; The Computer Language Shootout
;; http://shootout.alioth.debian.org/
;; Written by Dima Dorfman, 2004
;; Slightly improved by Sven Hartrumpf, 2005-2006
;; Adapted for gambit-c by Paul Berg

(declare (standard-bindings) (block) (not safe) (not interrupts-enabled) (fixnum))

(define (1+ i) (+ i 1))
(define (1- i) (- i 1))

(define vector-for-each (lambda (pred v)
                          (do ((i 0 (1+ i))
                               (v-length (vector-length v)))
                              ((>= i v-length))
                            (pred (vector-ref v i)))))

(define (vector-reverse-slice! v i j)
  (do ((i i (1+ i))
       (j (1- j) (1- j))) ; exclude position j
      ((<= j i))
    (vector-swap! v i j)))

(define (vector-swap! v i j)
  (let ((t (vector-ref v i)))
    (vector-set! v i (vector-ref v j))
    (vector-set! v j t)))

(define (count-flips pi)
  (do ((rho (vector-copy pi))
       (i 0 (1+ i)))
      ((= (vector-ref rho 0) 0) i)
    (vector-reverse-slice! rho 0 (1+ (vector-ref rho 0)))))

(define (fannkuch n)
  (let ((pi (do ((pi (make-vector n))
                 (i 0 (1+ i)))
                ((= i n) pi)
              (vector-set! pi i i)))
        (r n)
        (count (make-vector n)))
    (let loop ((flips 0)
               (perms 0))
      (cond ((< perms 30)
             (vector-for-each (lambda (x)
                                (display (1+ x)))
                              pi)
             (newline)))
      (do ()
          ((= r 1))
        (vector-set! count (1- r) r)
        (set! r (1- r)))
      (let ((flips2 (max (count-flips pi) flips)))
        (let ((result
               (let loop2 ()
                 (if (= r n)
                     flips2
                     (let ((perm0 (vector-ref pi 0)))
                       (do ((i 0))
                           ((>= i r))
                         (let ((j (1+ i)))
                           (vector-set! pi i (vector-ref pi j))
                           (set! i j)))
                       (vector-set! pi r perm0)
                       (vector-set! count r (1- (vector-ref count r)))
                       (if (<= (vector-ref count r) 0)
                           (begin
                             (set! r (1+ r))
                             (loop2))
                           #f))))))
          (or result
              (loop flips2 (1+ perms))))))))

(define (main arg)
  (let ((n (string->number arg)))
    (if (not (integer? n))
        (begin (display "An integer is required") (newline) 2)
        (display (string-append "Pfannkuchen(" arg ") = " (number->string (fannkuch n)) "\n")))))


;; The Computer Language Shootout
;; http://shootout.alioth.debian.org/
;;
;; Adapted for Gambit-c by Paul Berg
;; Originally contributed by Anthony Borla

(declare (standard-bindings) (block) (not safe) (not interrupts-enabled))

(define +limit-sqr+ 4.0)
(define +iterations+ 50)

(define (mandelbrot iterations x y n)
  (let ((cr (- (/ (* 2.0 x) n) 1.5))
        (ci (- (/ (* 2.0 y) n) 1.0)))
    (let loop ((i 0) (zr 0.0) (zi 0.0))
      (let ((zrq (* zr zr)) (ziq (* zi zi)))
        (cond
         ((> i iterations) 1)
         ((> (+ zrq ziq) +limit-sqr+) 0)
         (else (loop (+ 1 i) (+ (- zrq ziq) cr) (+ (* 2.0 zr zi) ci)))) ))))

(define (main args)
  (let ((n (string->number args))
        (bitnum 0)
        (byteacc 0))

    (display (string-append "P4\n" (number->string n) " " (number->string n) "\n"))

    (let loop-y ((y 0))
      (if (<= y (- n 1))
          (begin
            (let loop-x ((x 0))
              (if (<= x (- n 1))
                  (begin
                    (set! bitnum (+ 1 bitnum))
                    (set! byteacc (+ (* 2 byteacc) (mandelbrot +iterations+ x y n)))

                    (cond
                     ((= bitnum 8)
                      (write-char (integer->char byteacc))
                      (set! bitnum 0)
                      (set! byteacc 0))

                     ((= x (- n 1))
                      (write-char (integer->char (* byteacc (expt 2 (- 8 (modulo n 8))))))
                      (set! bitnum 0)
                      (set! byteacc 0)))

                    (loop-x (+ 1 x)))))

            (loop-y (+ 1 y)))))))



; The Computer Language Benchmarks Game
; http://shootout.alioth.debian.org/
; contributed by Carson Reynolds 2007/6/5

(define (iter in sum)
  (if (eof-object? in)
      sum
      (iter (read) (+ in sum))))

(display (iter (read) 0))
(newline)
